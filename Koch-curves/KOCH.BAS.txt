10 REM ***KROMME VAN HELGE VON KOCH***
20 REM ***NAAM : KOCH***
30 SCREEN 2 : CLS : PI=3.14159265
40 WINDOW (-0.1,-0.4)-(1.1,0.5)
50 P=4 : DIM T(P) : REM ***KEUZE ORDE***
60 H=3^(-P) : PSET(0,0)
70 FOR N=0 TO 4^P - 1
80 REM ***NOTATIE IN VIERTALLIG STELSEL***
90 M=N : FOR L=0 TO P-1
100 T(L)=M MOD 4 : M=M\4 : NEXT L
110 REM ***BEPALING RICHTING LIJNSTUK N***
120 S=0 : FOR K=0 TO P-1
130 S=S+(T(K)+1) MOD 3 - 1
140 NEXT K
150 REM ***TEKENING LIJNSTUK N***
160 X=X+COS(PI*S/3)*H
170 Y=Y+SIN(PI*S/3)*H
180 LINE -(X,Y)
190 NEXT N : BEEP : A$=INPUT$(1) : END
